plugins {
    id 'java-library'  //distinguishes between api and implementation
    id 'maven-publish'
    id 'io.freefair.aspectj' version '6.4.1'
}

group 'com.immersive'
version '1.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}
//this tells the ajc compiler to apply aspect to classes in these directories
//aspect itself must remain in src/main/aspectj folder
sourceSets.main.aspectj.srcDir "src/main/java"
sourceSets.main.java.srcDirs = files()
sourceSets.test.aspectj.srcDir "src/test/java"
sourceSets.test.java.srcDirs = files()

dependencies {
    implementation group: 'org.aspectj', name: 'aspectjrt', version: '1.9.6'
    //necessary dependencies included in .jar
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation group: 'org.jetbrains', name: 'annotations', version: '21.0.1'
    //unit testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

//provide .java classes in sources.jar
java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}